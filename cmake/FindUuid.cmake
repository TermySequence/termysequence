INCLUDE(FindPackageHandleStandardArgs)

PKG_CHECK_MODULES(PC_Uuid QUIET uuid)

FIND_LIBRARY(Uuid_LIBRARY
  NAMES uuid
  PATHS ${PC_Uuid_LIBRARY_DIRS}
)
FIND_PATH(Uuid_INCLUDE_DIR
  NAMES uuid.h
  PATHS ${PC_Uuid_INCLUDE_DIRS}
  PATH_SUFFIXES uuid
)

IF (APPLE)
  SET(Uuid_REQUIRED_VARS Uuid_INCLUDE_DIR)
ELSE()
  SET(Uuid_REQUIRED_VARS Uuid_INCLUDE_DIR Uuid_LIBRARY)
ENDIF()

SET(Uuid_VERSION ${PC_Uuid_VERSION})

FIND_PACKAGE_HANDLE_STANDARD_ARGS(Uuid
  REQUIRED_VARS ${Uuid_REQUIRED_VARS}
  VERSION_VAR Uuid_VERSION
)

IF (Uuid_FOUND)
  IF (Uuid_LIBRARY STREQUAL "Uuid_LIBRARY-NOTFOUND")
    ADD_LIBRARY(l::Uuid INTERFACE IMPORTED)
  ELSE()
    ADD_LIBRARY(l::Uuid UNKNOWN IMPORTED)
    SET_TARGET_PROPERTIES(l::Uuid PROPERTIES
      IMPORTED_LOCATION "${Uuid_LIBRARY}"
    )
  ENDIF()

  SET_TARGET_PROPERTIES(l::Uuid PROPERTIES
    INTERFACE_COMPILE_OPTIONS "${PC_Uuid_CFLAGS_OTHER}"
    INTERFACE_INCLUDE_DIRECTORIES "${Uuid_INCLUDE_DIR}"
  )
ENDIF()
