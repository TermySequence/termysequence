INCLUDE(FindPackageHandleStandardArgs)

IF (Fuse_FIND_VERSION_MAJOR EQUAL 3)
  SET(Fuse_NAME fuse3)
ELSE()
  SET(Fuse_NAME fuse)
ENDIF()

PKG_CHECK_MODULES(PC_Fuse QUIET ${Fuse_NAME})

FIND_LIBRARY(Fuse_LIBRARY
  NAMES ${Fuse_NAME}
  PATHS ${PC_Fuse_LIBRARY_DIRS}
)
FIND_PATH(Fuse_INCLUDE_DIR
  NAMES fuse_common.h
  PATHS ${PC_Fuse_INCLUDE_DIRS}
  PATH_SUFFIXES ${Fuse_NAME}
)

SET(Fuse_VERSION ${PC_Fuse_VERSION})
IF (Fuse_INCLUDE_DIR AND EXISTS "${Fuse_INCLUDE_DIR}/fuse_common.h")
  FILE(STRINGS "${Fuse_INCLUDE_DIR}/fuse_common.h" _VERS_H REGEX "#define FUSE_MAJOR_VERSION +[0-9]+")
  STRING(REGEX REPLACE ".* ([0-9]+).*" "\\1" _MAJOR_VERSION "${_VERS_H}")

  FILE(STRINGS "${Fuse_INCLUDE_DIR}/fuse_common.h" _VERS_H REGEX "#define FUSE_MINOR_VERSION +[0-9]+")
  STRING(REGEX REPLACE ".* ([0-9]+).*" "\\1" _MINOR_VERSION "${_VERS_H}")

  SET(Fuse_VERSION "${_MAJOR_VERSION}.${_MINOR_VERSION}")
ENDIF()

FIND_PACKAGE_HANDLE_STANDARD_ARGS(Fuse
  REQUIRED_VARS Fuse_LIBRARY Fuse_INCLUDE_DIR
  VERSION_VAR Fuse_VERSION
)

IF (Fuse_FOUND)
  ADD_LIBRARY(l::Fuse UNKNOWN IMPORTED)
  SET_TARGET_PROPERTIES(l::Fuse PROPERTIES
    IMPORTED_LOCATION "${Fuse_LIBRARY}"
    INTERFACE_COMPILE_OPTIONS "${PC_Fuse_CFLAGS_OTHER}"
    INTERFACE_INCLUDE_DIRECTORIES "${Fuse_INCLUDE_DIR}"
  )
ENDIF()
