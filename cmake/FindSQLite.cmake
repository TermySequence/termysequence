INCLUDE(FindPackageHandleStandardArgs)

PKG_CHECK_MODULES(PC_SQLite QUIET sqlite3)

FIND_LIBRARY(SQLite_LIBRARY
  NAMES sqlite3
  PATHS ${PC_SQLite_LIBRARY_DIRS}
)
FIND_PATH(SQLite_INCLUDE_DIR
  NAMES sqlite3.h
  PATHS ${PC_SQLite_INCLUDE_DIRS}
)

SET(SQLite_VERSION ${PC_SQLite_VERSION})
IF (NOT SQLite_VERSION AND SQLite_INCLUDE_DIR AND EXISTS "${SQLite_INCLUDE_DIR}/sqlite3.h")
  FILE(STRINGS "${SQLite_INCLUDE_DIR}/sqlite3.h" _VERS_H REGEX "#define SQLITE_VERSION +\"[0-9].+\"")
  STRING(REGEX REPLACE ".*\"([0-9][^ ]+)\".*" "\\1" SQLite_VERSION "${_VERS_H}")
ENDIF()

FIND_PACKAGE_HANDLE_STANDARD_ARGS(SQLite
  REQUIRED_VARS SQLite_LIBRARY SQLite_INCLUDE_DIR
  VERSION_VAR SQLite_VERSION
)

IF (SQLite_FOUND)
  ADD_LIBRARY(l::SQLite UNKNOWN IMPORTED)
  SET_TARGET_PROPERTIES(l::SQLite PROPERTIES
    IMPORTED_LOCATION "${SQLite_LIBRARY}"
    INTERFACE_COMPILE_OPTIONS "${PC_SQLite_CFLAGS_OTHER}"
    INTERFACE_INCLUDE_DIRECTORIES "${SQLite_INCLUDE_DIR}"
  )
ENDIF()
